plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group 'org.jigsawlabs'
version '0.1'

archivesBaseName = "fabric-wrapper"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        name = 'mojang'
        url = 'https://libraries.minecraft.net/'
    }
}

dependencies {

    //Logger
    implementation 'org.apache.logging.log4j:log4j-api:2.8.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.8.1'
    annotationProcessor 'org.apache.logging.log4j:log4j-core:2.8.1'

    //Annotations
    implementation 'org.jetbrains:annotations:19.0.0'

    //Json
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation ('org.sharegov:mjson:1.4.1') {
        transitive false
    }

    implementation 'net.minecrell:terminalconsoleappender:1.2.0'
    implementation 'com.mojang:authlib:2.1.28'
}

shadowJar {
    archiveClassifier.set('')
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

build.dependsOn shadowJar

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this task, sources will not be generated.
    withSourcesJar()
}

jar {
    manifest {
        attributes (
                'Main-Class': 'org.jigsawlabs.fabricwrapper.FabricWrapper'
        )
    }

    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}
